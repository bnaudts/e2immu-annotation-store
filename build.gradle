/*
 * e2immu: a static code analyser for effective and eventual immutability

 *
 * This program is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
 * more details. You should have received a copy of the GNU Lesser General Public
 * License along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'org.e2immu.analyser'
}

group 'org.e2immu'
version '0.1.2'

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/e2immu/e2immu-support")
    }
}

dependencies {
    implementation group: 'org.e2immu', name: 'e2immu-support', version: '0.1.2'      // LGPL 3.0
    
    implementation group: 'io.vertx', name: 'vertx-web', version: '4.0.3'
    implementation group: 'io.vertx', name: 'vertx-config', version: '4.0.3'

    implementation(files('annotation-xml-0.1.2.jar'))

    // TEST
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'                    // EPL v2.0
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    
    testImplementation group: 'io.vertx', name: 'vertx-junit5', version: '4.0.3'
    testImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.0.3'

    testImplementation group: 'org.e2immu', name: 'analyser', version: '0.1.2'      // LGPL 3.0

}

test {
    useJUnitPlatform()
}

application {
    mainClass.set('org.e2immu.kvstore.Store')

    // uncomment here to change port and read timing each time you execute
    //  gradle run
    //applicationDefaultJvmArgs = ['-De2immu-port=9999 -De2immu-read-within-millis=10000']
}


// the following block copies two system properties from Gradle's JVM to the JVM that starts the Store
// this allows us to execute
//   gradle run -De2immu-port=9999

run {
    systemProperty "e2immu-port", System.getProperty("e2immu-port")
    systemProperty "e2immu-read-within-millis", System.getProperty("e2immu-read-within-millis")
}

e2immu {
    debug = "INSPECT,BYTECODE_INSPECTOR,ANALYSER,DELAYED"
    jmods = 'java.base.jmod,java.logging.jmod'
    sourcePackages = "org.e2immu.kvstore"
    readAnnotatedAPIPackages = "org.e2immu.kvstoreaapi"
    writeAnnotationXML = true
    writeAnnotationXMLPackages = "org.e2immu."
    upload = true
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/e2immu/e2immu-annotation-store")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'org.e2immu'
            artifactId = 'e2immu-annotation-store'
            from components.java
        }
    }
}
