/*
 * e2immu: a static code analyser for effective and eventual immutability
 * Copyright 2020-2021, Bart Naudts, https://www.e2immu.org
 *
 * This program is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
 * more details. You should have received a copy of the GNU Lesser General Public
 * License along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'org.e2immu.analyser'
}

group 'org.e2immu'
version '0.1.2'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/e2immu/e2immu-support")
    }
}

dependencies {
    implementation group: 'org.e2immu', name: 'e2immu-support', version: '0.1.0'      // LGPL 3.0
    
    implementation group: 'io.vertx', name: 'vertx-web', version: '3.8.5'
    implementation group: 'io.vertx', name: 'vertx-config', version: '3.8.5'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'io.vertx', name: 'vertx-unit', version: '3.8.5'
    testImplementation group: 'io.vertx', name: 'vertx-web-client', version: '3.8.5'
}

application {
    mainClass.set('org.e2immu.kvstore.Store')

    // uncomment here to change port and read timing each time you execute
    //  gradle run
    //applicationDefaultJvmArgs = ['-De2immu-port=9999 -De2immu-read-within-millis=10000']
}


// the following block copies two system properties from Gradle's JVM to the JVM that starts the Store
// this allows us to execute
//   gradle run -De2immu-port=9999

run {
    systemProperty "e2immu-port", System.getProperty("e2immu-port")
    systemProperty "e2immu-read-within-millis", System.getProperty("e2immu-read-within-millis")
}

e2immu {
    debug = "INSPECT,BYTECODE,ANALYSER"
    jmods = 'java.base.jmod'
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/e2immu/e2immu-annotation-store")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'org.e2immu'
            artifactId = 'e2immu-annotation-store'
            from components.java
        }
    }
}
